;---------------------------------------------------------------------
;
; Brief description:
;
; This function returns:
;
;  - For lines: s is the line emissivity function evaluated at the
;    provided values Ne0, Te0 and r0, using the provided function G.
;          
;  - For EUV bands: s is the FBE function evaluated at the
;    provided values for Ne0 and Te0, using the provided function TRF.
;
; G and TRF are tri-linearly interpolated onto the grid (Ne,Te,r).
;
;
; INPUTS: values for Ne, Te, rad and fip_factor (default value is 1).
;
; Note: fip_factor=1 means using the abundance set assumed in
; computing the loop-up table for G/TRF.
;
; OUTPUT:
;
; RESULT = [s0]
;
; With the s0 value returned in the following units:
;
; For lines: Emissivity [erg cm-3 sec-1 sr-1]
; or
; For EUV bands: FBE    [ph  cm-3 sec-1 sr-1]
;
; History:  V1.0, A.M. Vasquez, CLaSP, Spring-2018.
;           V1.1, F.A. Nuevo, IAFE, March-2020.
;                 Derivatives and Ne-loop were both removed
;           V1.2. A.M.Vasquez.
;                 Eliminated unnecesary common block.
;---------------------------------------------------------------------
function s_function, Ne0, Te0
  common parameters, r0, fip_factor, Tem, Nem, SigTe, SigNe, q
  common dimensions, NTe, NNe
  NTe = 1
  NNe = 1
  if (size(Te0))(0) eq 1 then NTe = (size(Te0))(1)
  if (size(Ne0))(0) eq 1 then NNe = (size(Ne0))(1)
; stop
; Linearly interpolate G from the look-up table
  RESULT_s = dblarr(NTe,NNe)
; Evaluate G(Te0,Ne0)
  RESULT_g = g_function(Te0,Ne0) ; [ERG(or PH) cm+3 sec-1]; NOTE: array NTe x NNe.
; Compute emissivity s(Te0,Ne0), assuming isotropic emission:
    for iTe=0,NTe-1 do result_s[iTe,*]=(fip_factor/4./!pi)*Ne0^2*result_g[iTe,*] ; [ERG(or PH) cm-3 sec-1 sr-1]
  return, RESULT_s
end
